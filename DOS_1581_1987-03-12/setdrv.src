	.messg "setdrv"
	.subttl "setdrv"
        .page  
onedrv  lda  f2cnt       
        sta  f1cnt       
        lda  #1          
        sta  f2cnt       
        sta  f2ptr       
;set up all drives from f2cnt
alldrs  ldx  #0         ; ...into file entry table...
ad10    stx  f1ptr      ; ...on sector ptr byte
        lda  filtbl,x    
        jsr  setdrv      
        ldx  f1ptr       
        sta  filtbl,x   ; incr ptr past ":"
        tya     	; bits rep drives
        sta  fildrv,x   ; bit7: default
        inx     	; bit0: drive #
        cpx  f2cnt       
        bcc  ad10        

        rts      
;---------------------------------------
;  set drive number
;  determines drive # from text or
;  uses default (-d)
;  a: in,out: index, cmdbuf
;  y: in: default drive
;    out: drive number, - if default
;---------------------------------------
setdrv  tax     	; x= cmdbuf index
        ldy  #0         ; set default drive to zero
        lda  #':         
        cmp  cmdbuf+1,x ; for xxx:file
        beq  sd40       ;       ^ 

        cmp  cmdbuf,x   ; for xxx:file
        bne  sd50       ;        ^

        inx     	; found ":", so...
sd20    tya     	; drive= default
sd22    and  #1         ; convert to numeric
sd24    tay     	; restore drive
        txa     	; a=index & xxxxfile
        rts     	;               ^

sd40    lda  cmdbuf,x    
        inx     	;  xxx:file
        inx     	;    --^
        cmp  #'0        ; for xx0:file
        beq  sd22       ;         ^

	cmp  #'1	; drive one?
	bne  sd20

	jmp  bcerr      ; bad cmd error

sd50    tya     	; for xxx,file or xx=file
        ora  #$80       ;         ^          ^
        and  #$81       ; drive= -default
        bne  sd24       ; finish testing
	.pag             
setany  lda  #0         ; set drv frm any config
        sta  image       
        ldy  filtbl      
sa05    lda  (cb),y      
        jsr  tst0v1      
        bpl  sa20        

        iny      
        cpy  cmdsiz      
        bcs  sa10        

        ldy  cmdsiz      
        dey      
        bne  sa05        

sa10    dec  image       
sa20    jmp  setlds      

;set ptrs to one file stream & chk type

fs1set  ldy  #0          
        lda  f1cnt       
        cmp  f2cnt       
        beq  fs15        

        dec  f2cnt       
        ldy  f2cnt       
        lda  filtbl,y    
        tay      
        lda  (cb),y      
        ldy  #ntypes-1   
fs10    cmp  typlst,y    
        beq  fs15        

        dey      
        bne  fs10        

fs15    tya      
        sta  typflg      
        rts      

;test char in accum for "0" or "1"

tst0v1  cmp  #'0         
        beq  t0v1        

        cmp  #'1         
        beq  t0v1        

        ora  #$80        
t0v1    and  #$81        
        rts      
