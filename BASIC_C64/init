.PAG 'INIT'
PANIC	JSR CLSCHN      ;WARM START BASIC...
	LDA #0          ;CLEAR CHANNELS
	STA CHANNL
	JSR STKINI      ;RESTORE STACK
	CLI             ;ENABLE IRQ'S
.SKI 2
READY	LDX #$80
	JMP (IERROR)
NERROR	TXA             ;GET  HIGH BIT
	BMI NREADY
	JMP NERROX
NREADY	JMP READYX
.SKI 2
INIT	JSR INITV       ;GO INIT VECTORS
	JSR INITCZ      ;GO INIT CHARGET & Z-PAGE
	JSR INITMS      ;GO PRINT INITILIZATION MESSAGES
	LDX #STKEND-256 ;SET UP END OF STACK
	TXS
	BNE READY       ;JMP...READY
.SKI 4
INITAT	INC CHRGET+7
	BNE CHDGOT
	INC CHRGET+8
CHDGOT	LDA 60000
	CMP #':
	BCS CHDRTS
	CMP #' 
	BEQ INITAT
	SEC
	SBC #'0
	SEC
	SBC #$D0
CHDRTS	RTS
	.BYT 128,79,199,82,88
.SKI 6
INITCZ	LDA #76
	STA JMPER
	STA USRPOK
	LDA #<FCERR
	LDY #>FCERR
	STA USRPOK+1
	STY USRPOK+2
	LDA #<GIVAYF
	LDY #>GIVAYF
	STA ADRAY2
	STY ADRAY2+1
	LDA #<FLPINT
	LDY #>FLPINT
	STA ADRAY1
	STY ADRAY1+1
	LDX #INITCZ-INITAT-1
MOVCHG	LDA INITAT,X
	STA CHRGET,X
	DEX
	BPL MOVCHG
	LDA #STRSIZ
	STA FOUR6
	LDA #0
	STA BITS
	STA CHANNL
	STA LASTPT+1
	LDX #1
	STX BUF-3
	STX BUF-4
	LDX #TEMPST
	STX TEMPPT
	SEC             ;READ BOTTOM OF MEMORY
	JSR $FF9C
	STX TXTTAB      ;NOW TXTAB HAS IT
	STY TXTTAB+1
	SEC
	JSR $FF99       ;READ TOP OF MEMORY
USEDEF	STX MEMSIZ
	STY MEMSIZ+1
	STX FRETOP
	STY FRETOP+1
	LDY #0
	TYA
	STA (TXTTAB)Y
	INC TXTTAB
	BNE INIT20
	INC TXTTAB+1
INIT20	RTS
.SKI 6
INITMS	LDA TXTTAB
	LDY TXTTAB+1
	JSR REASON
	LDA #<FREMES
	LDY #>FREMES
	JSR STROUT
	LDA MEMSIZ
	SEC
	SBC TXTTAB
	TAX
	LDA MEMSIZ+1
	SBC TXTTAB+1
	JSR LINPRT
	LDA #<WORDS
	LDY #>WORDS
	JSR STROUT
	JMP SCRTCH
.SKI 4
BVTRS	.WOR NERROR,NMAIN,NCRNCH,NQPLOP,NGONE,NEVAL
;
INITV	LDX #INITV-BVTRS-1 ;INIT VECTORS
INITV1	LDA BVTRS,X
	STA IERROR,X
	DEX
	BPL INITV1
	RTS
CHKE0	.BYT $00
.SKI 4
WORDS	.BYT ' BASIC BYTES FREE',13,0
FREMES	.BYT 147,13,'    **** COMMODORE 64 BASIC V2 ****'
	.BYT 13,13,' 64K RAM SYSTEM  ',0
	.BYT 0
; PPACH - PRINT# PATCH TO COOUT (SAVE .A)
;
PPACH	PHA
	JSR $FFC9
	TAX             ;SAVE ERROR CODE
	PLA
	BCC PPACH0      ;NO ERROR....
	TXA             ;ERROR CODE
PPACH0	RTS
.END
;RSR 8/10/80 UPDATE PANIC :REM COULD USE IN ERROR ROUTINE
;RSR 2/08/82 MODIFY FOR VIC-40 RELEASE
;RSR 4/15/82 ADD ADVERTISING SIGN-ON
;RSR 7/02/82 ADD PRINT# PATCH
