.PAG 'CASSETTE READ'
READ	LDX T2H
	LDY #$FF
	TYA
	SBC T2L
	CPX T2H
	BNE READ
RD1	STX TEMP
	TAX
	STY T2L
	STY T2H
	TYA
	SBC TEMP
	STX TEMP
	LSR A
	ROR TEMP
	LSR A
RADS	ROR TEMP
RD3	LDA CMP0
	CLC
	ADC #60
	BIT PIA
	BIT PIAL
	CMP TEMP
	BCS RDBK
	LDX DPSW
	BEQ RJDJ
JRADJ	JMP RADJ
RJDJ	LDX PCNTR
	BMI JRAD2
	LDX #0
	ADC #48
	ADC CMP0
	CMP TEMP
	BCS RADX2
	INX
	ADC #38
	ADC CMP0
	CMP TEMP
	BCS RADL
	ADC #44
	ADC CMP0
	CMP TEMP
	BCC SRER
JRAD2	JMP RAD2
SRER	LDA SNSW1
	BEQ RDBK
	STA RER
	BNE RDBK
RADX2	INC REZ
	BCS RAD5
RADL	DEC REZ
RAD5	SEC
	SBC #19
	SBC TEMP
	ADC SVXT
	STA SVXT
	LDA FIRT
	EOR #1
	STA FIRT
	BEQ RAD3
	STX DATA
RDBK	LDA SNSW1
	BEQ RADBK
	BIT IFR
	BVC RADBK
	LDA #0
	STA FIRT
	LDA PCNTR
	BPL RAD4
	BMI JRAD2
RADP	LDX #166
	JSR STT1
	LDA PRTY
	BNE SRER
RADBK	JMP PREND
RAD3	LDA SVXT
	BEQ ROUT1
	BMI ROUT2
	DEC CMP0
	DEC CMP0
ROUT2	INC CMP0
ROUT1	LDA #0
	STA SVXT
	CPX DATA
	BNE RAD4
	TXA
	BNE SRER
	LDA REZ
	BMI RDBK
	CMP #16
	BCC RDBK
RAD3G	STA SYNO
	BCS RDBK
RAD4	TXA
	EOR PRTY
	STA PRTY
	LDA SNSW1
	BEQ RADBK
	DEC PCNTR
	BMI RADP
	LSR DATA
	ROR MYCH
	LDX #218
	JSR STT1
	JMP PREND
RAD2	LDA SYNO
	BEQ RAD2Y
	LDA SNSW1
	BEQ RAD2X
RAD2Y	LDA PCNTR
	BMI RAD2X
	JMP RADL
RAD2X	LSR TEMP
	LDA #147
	SEC
	SBC TEMP
	ADC CMP0
	ASL A
	TAX
	JSR STT1
	INC DPSW
RADQ	LDA SNSW1
	BNE RADQ2
RADQ1	LDA SYNO
	BEQ RDBK2
	STA RER
	LDA #0
	STA SYNO
	LDA #$C0
	STA IER
	STA SNSW1
RADQ2	LDA SYNO
	STA DIFF
	BEQ RADK
	LDA #0
	STA SNSW1
	LDA #$40
	STA IER
RADK	LDA MYCH
	STA OCHAR
	LDA RER
RADR1	ORA REZ
	STA PRP
RDBK2	JMP PREND
RADJ	JSR NEWCH
	STA DPSW
	LDX #218
	JSR STT1
	LDA FSBLK
	BEQ RD15
	STA SHCNL
SPERR=16
CKERR=32
SBERR=4
LBERR=8
RD15	LDA #$F
	BIT RDFLG
	BPL RD20
	LDA DIFF
	BNE RD12
	LDX FSBLK
	DEX
	BNE RD10
;LONG BLOCK ERROR
	LDA #LBERR
	JSR UDST
	BNE RD10
RD12	LDA #0
	STA RDFLG
RD10	JMP PREND
RD20	BVS RD60
	BNE RD200
	LDA DIFF
	BNE RD10
	LDA PRP
	BNE RD10
	LDA SHCNL
	LSR A
	LDA OCHAR
	BMI RD22
	BCC RD40
	CLC
RD22	BCS RD40
	AND #$F
	STA RDFLG
RD200	DEC RDFLG
	BNE RD10
	LDA #$40
	STA RDFLG
	JSR RD300
	LDA #0
	STA SHCNH
	BEQ RD10
RD40	LDA #$80
	STA RDFLG
	BNE RD10
RD60	LDA DIFF
	BEQ RD70
	LDA #SBERR
; SHORT BLOCK ERROR
	JSR UDST
	LDA #0
	JMP RD161
RD70	JSR WRT62
	BNE *+5
	JMP RD160
	LDX SHCNL
	DEX
	BEQ RD58
	LDA VERCK
	BEQ RD80
	LDY #0
	LDA OCHAR
	CMP (SAL)Y
	BEQ RD80
	LDA #1
	STA PRP
RD80	LDA PRP
	BEQ RD59
	LDX #61
	CPX PTR1
	BCC RD55
RD56	LDX PTR1
	LDA SAH
	STA BAD+1,X
	LDA SAL
	STA BAD,X
	INX
	INX
	STX PTR1
	JMP RD59
RD58	LDX PTR2
	CPX PTR1
	BEQ RD90
	LDA SAL
	CMP BAD,X
	BNE RD90
	LDA SAH
	CMP BAD+1,X
	BNE RD90
	INC PTR2
	INC PTR2
	LDA VERCK
	BEQ RD52
	LDA OCHAR
	LDY #0
	CMP (SAL)Y
	BEQ RD90
	LDA #1
	STA PRP
RD52	LDA PRP
	BEQ RD59
;SECOND PASS ERR
RD55	LDA #SPERR
	JSR UDST
	BNE RD90
RD59	LDA VERCK
	BNE RD90
	LDA OCHAR
	LDY #0
	STA (SAL)Y
RD90	INC SAL
	BNE RD180
	INC SAH
	BNE RD180
RD160	LDA #$80
RD161	STA RDFLG
	LDX FSBLK
	DEX
	BMI RD167
	STX FSBLK
RD167	DEC SHCNL
	BEQ RD175
	LDA PTR1
	BNE RD180
	STA FSBLK
	BEQ RD180
RD175	JSR TNIF
	JSR RD300
	LDY #0
	STY SHCNH
	JSR VPRTY
	LDA SHCNH
	EOR OCHAR
	BEQ RD180
	LDA #CKERR
;CHKSUM ERROR
	JSR UDST
RD180	JMP PREND
RD300	LDA STAH
	STA SAH
	LDA STAL
	STA SAL
	RTS
UDST	ORA SATUS
	STA SATUS
	RTS
NEWCH	LDA #8
	STA PCNTR
	LDA #0
	STA FIRT
	STA RER
	STA PRTY
	STA REZ
	RTS
.FILE OB6SRC
