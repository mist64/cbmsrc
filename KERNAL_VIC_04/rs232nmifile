.PAGE 'NMI HANDLER'
NMI	SEI             ;NO IRQ'S ALLOWED...
	JMP (NMINV)     ;...COULD MESS UP CASSETTES
NNMI	PHA             ;SAVE 6502 REGS
	TXA
	PHA
	TYA
	PHA
NNMI10	LDA D1IFR       ;CHECK IF REAL NMI...
	BPL NMIRTN      ;...NO
;
	AND D1IER       ;SHOW ONLY ENABLES
	TAX             ;SAVE IN .X FOR LATTER
	AND #$02        ;CHECK IF RESTORE KEY...
	BEQ NNMI20      ;...NO
;
NNMI18	JSR A0INT       ;CHECK IF $A0 IN...
	BNE NNMI19      ;...NO
	JMP ($A002)     ;...YES
;
; CHECK FOR STOP KEY DOWN
;
NNMI19	BIT D1ORAH      ;CLR CA1 FLAG
	JSR UDTIM
	JSR STOP
	BNE NMIRTN      ;NO STOP KEY
;
; TIMB - WHERE SYSTEM GOES ON A BRK INSTRUCTION
;
TIMB	JSR RESTOR      ;RESTORE SYSTEM INDIRECTS
	JSR IOINIT      ;RESTORE I/O FOR BASIC
	JSR CINT        ;RESTORE SCREEN FOR BASIC
	JMP ($C002)     ;...NO, SO BASIC WARM START
.SKI 2
; DISABLE NMI'S UNTILL READY
;  SAVE ON STACK
;
NNMI20	LDA D1IER       ;SAVE CURRENT ENABLES...
	ORA #$80        ;SO WE CAN ENABLE ON RESTORE
	PHA
	LDA #$7F        ;KILL THE REST
	STA D1IER
;
;
; T1 NMI CHECK - TRANSMITT A BIT
;
	TXA
	AND #$40        ;CHECK FOR T1
	BEQ NNMI30      ;NO...
;
	LDA #$CE        ;FIXED PCR HERE
; WILL TURN OFF YOUR CASS MOTOR!
	ORA NXTBIT      ;LOAD DATA AND...
	STA D1PCR       ;...SEND IT
;
	LDA D1T1L       ;KILL REQUEST
;
	PLA             ;RESTORE NMI'S
	STA D1IER       ;READY FOR NEXT...
;
	JSR RSTRAB      ;GO CALC INFO (CODE COULD BE IN LINE)
NMIRTN	JMP NMIRTI
;
.SKI 2
; T2 NMI CHECK - RECIEVE A BIT
;
NNMI30	TXA
	AND #$20        ;MASK TO T2
	BEQ NNMI40      ;NO...
;
	LDA D1ORB       ;GET DATA IN
	AND #01         ;MASK OFF...
	STA INBIT       ;...SAVE FOR LATTER
;
; UPDATE T2 FOR MID BIT CHECK
;   (WORST CASE <255 US TO HERE)
;
	LDA D1T2L       ;CALC NEW TIME & CLR NMI
	SBC #22         ;TIME FOR THIS LOOP
	ADC BAUDOF
	STA D1T2L
	LDA D1T2H
	ADC BAUDOF+1
	STA D1T2H
;
;
	PLA             ;RESTORE NMI'S
	STA D1IER
;
	JSR RSRCVR      ;GO SHIFT IN...
	JMP NMIRTI
.SKI 2
; CB1 NMI HANDLER - RECIEVE A START BIT
;
NNMI40	TXA             ;CHECK FOR EDGE
	AND #$10        ;ON CB1...
	BEQ NMIRTI      ;NO...
;
; CHECK FOR NOISE ?
;
	LDA M51CTR      ;GET LENGTH TO CHECK FOR
	AND #$0F
	BNE NNMI42      ;USE TABLES
;
; GET BAUD RATE FROM ELSE WHERE ###NEED CODE###
;
NNMI42	ASL A           ;GET DATA FROM BAUD TABLE
	TAX
	LDA BAUDO-2,X   ;MOVE TO TIMER
	STA D1T2L
	LDA BAUDO-1,X
	STA D1T2H
;
	LDA D1ORB       ;KILL CB1 EDGE NMI
	PLA             ;RESTORE NMI'S
	ORA #$20        ;ENABLE T2 NMI
	AND #$EF        ;DISABLE EDGE ON CB1
	STA D1IER
;
	LDX BITNUM      ;GET #OF BITS IN
	STX BITCI       ;PUT IN RCVRCNT
;
.SKI 2
NMIRTI	                ;RESTORE 6502 REGS
PREND	PLA             ;BECAUSE OF MISSING SCREEN EDITOR
	TAY
	PLA
	TAX
	PLA
	RTI
.SKI 4
; BAUDO TABLE CONTAINS VALUES
;  FOR 1.023E6/BAUD RATE/2
;
BAUDO	.WOR 10230-CBIT ; 50 BAUD
	.WOR  6820-CBIT ;   75   BAUD
	.WOR  4650-CBIT ;  110   BAUD
	.WOR  3800-CBIT ;  134.6 BAUD
	.WOR  3410-CBIT ;  150   BAUD
	.WOR  1705-CBIT ;  300   BAUD
	.WOR  853-CBIT  ;  600   BAUD
	.WOR  426-CBIT  ; 1200   BAUD
	.WOR  284-CBIT  ; 1800   BAUD
	.WOR  213-CBIT  ; 2400   BAUD
	.WOR  142-CBIT  ; 3600   BAUD
;
; CBIT - AN ADJUSTMENT TO MAKE NEXT T2 HIT NEAR CENTER
;   OF THE NEXT BIT.
;   APROX THE TIME TO SERVICE A CB1 NMI
CBIT	= 100           ;US
.END
; RSR 8/2/80 - ROUTINE FOR PANIC
; RSR 8/8/80 - PANIC & STOP KEY
; RSR 8/12/80 - CHANGE FOR A0INT A SUBROUTINE
; RSR 8/19/80 - ADD RS-232 CHECKS
; RSR 8/21/80 - MODIFY RS-232
; RSR 8/29/80 - CHANGE PANIC ORDER FOR JACK
; RSR 8/30/80 - ADD T2
; RSR 9/22/80 - ADD 1800 BAUD OPPS!
