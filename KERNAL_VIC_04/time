.PAG 'TIME FUNCTION'
;***********************************
;*                                 *
;* TIME                            *
;*                                 *
;*CONSISTS OF THREE FUNCTIONS:     *
;* (1) UDTIM-- UPDATE TIME. USUALLY*
;*     CALLED EVERY 60TH SECOND.   *
;* (2) SETTIM-- SET TIME. .Y=MSD,  *
;*     .X=NEXT SIGNIFICANT,.A=LSD  *
;* (3) RDTIM-- READ TIME. .Y=MSD,  *
;*     .X=NEXT SIGNIFICANT,.A=LSD  *
;*                                 *
;***********************************
;INTERRUPTS ARE COMING FROM TV HORIZONTAL
;WHICH IS CLOSE TO 60HZ BUT A LITTLE OVER.
;CRFAC COUNTS UP THEN SKIPS ONE UPDATE
;OF THE TIME REGISTER.
;
UDTIM	LDX #0          ;PRE-LOAD FOR LATER
; INC CRFAC
; LDA CRFAC
; BNE UD10
; INC CRFAC+1
; UD10 CMP #$6F
; BNE UD20
; LDA CRFAC+1
; CMP #2
; BEQ UD50
;
;HERE WE PROCEED WITH AN INCREMENT
;OF THE TIME REGISTER.
;
UD20	INC TIME+2
	BNE UD30
	INC TIME+1
	BNE UD30
	INC TIME
;
;HERE WE CHECK FOR ROLL-OVER 23:59:59
;AND RESET THE CLOCK TO ZERO IF TRUE
;
;24:00:00 = 5184000 JIFFIES
;=79*65536+26*256+0
;=$4F*65536+$1A*256+$0+CORRECTION
;
;FINAL CORRECTION IS +1/60TH IN
;24 HOURS.
;
UD30	SEC
	LDA TIME+2
	SBC #$01
	LDA TIME+1
	SBC #$1A
	LDA TIME
	SBC #$4F
	BCC UD60
;
;TIME HAS ROLLED--ZERO REGISTER
;
	STX TIME
	STX TIME+1
	STX TIME+2
; BCS UD60 ;BRANCH ALWAYS
;
;COR. FACTOR ROLLED--ZERO IT
;
; UD50 STX CRFAC
; STX CRFAC+1
;
;SET STOP KEY FLAG HERE
;
UD60	LDA D2ORA       ;WAIT FOR IT TO SETTLE
	CMP D2ORA
	BNE UD60        ;STILL BOUNCING
	JMP STPTCH      ;JUMP TO PATCH CHECKING FOR BOTH SHIFT KEYS
;
; THE FOLLOWING 3 BYTES WERE REPLACED WITH THE PRECEEDING JMP
;
;  STA STKEY ;SAVE FOR OTHER ROUTINES
;  RTS
.SKI 5
RDTIM	SEI             ;KEEP TIME FROM ROLLING
	LDA TIME+2      ;GET LSD
	LDX TIME+1      ;GET NEXT MOST SIG.
	LDY TIME        ;GET MSD
.SKI 5
SETTIM	SEI             ;KEEP TIME FROM CHANGING
	STA TIME+2      ;STORE LSD
	STX TIME+1      ;NEXT MOST SIGNIFICANT
	STY TIME        ;STORE MSD
	CLI
	RTS
SKIPIT	= *             ;GO AROUND THIS PATCH
;
;*********************************************************************
;
;   PATCH TO CHECK FOR RIGHT SHIFT KEY  ON STOP KEY CHECK
;
;*********************************************************************
;
	*=KSP3
STPTCH
	TAX             ;SAVE STOP KEY ROW
	SEC
	ROL COLM        ;LOOK AT NEXT ROW FOR THIS COLUMN
	LDA ROWS
	ROR COLM        ;PUT IT BACK THE WAY IT WAS
	AND #$40        ;CHECK RIGHT SHIFT KEY BIT
	BEQ ITSSET      ;BRANCH IF RIGHT SHIFT IS DOWN
;
	STX STKEY       ;ELSE LEAVE IT AS IT IS
	RTS
;
ITSSET	TXA
	AND #$FD        ;CLEAR THE SHIFT BIT
	STA STKEY       ;ITS NOT THE STOP KEY ALONE
	RTS
;
KSP4	= *
	*=SKIPIT
.END
; RSR 8/21/80 REMOVE CRFAC CHANGE STOP
; JM 1/21/80 FIX SO SHIT/STOP GIVE RUN FROM RIGHT
