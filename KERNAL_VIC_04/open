.PAG 'OPEN FILE'
;***********************************
;*                                 *
;* OPEN FUNCTION                   *
;*                                 *
;* CREATES AN ENTRY IN THE LOGICAL *
;* FILES TABLES CONSISTING OF      *
;* LOGICAL FILE NUMBER--LA, DEVICE *
;* NUMBER--FA, AND SECONDARY CMD-- *
;* SA.                             *
;*                                 *
;* A FILE NAME DESCRIPTOR, FNADR & *
;* FNLEN ARE PASSED TO THIS ROUTINE*
;*                                 *
;***********************************
;
NOPEN	LDX LA          ;CHECK FILE #
	BNE OP98        ;IS NOT THE KEYBOARD
;
	JMP ERROR6      ;NOT INPUT FILE...
;
OP98	JSR LOOKUP      ;SEE IF IN TABLE
	BNE OP100       ;NOT FOUND...O.K.
;
	JMP ERROR2      ;FILE OPEN
;
OP100	LDX LDTND       ;LOGICAL DEVICE TABLE END
	CPX #10         ;MAXIMUM # OF OPEN FILES
	BCC OP110       ;LESS THAN 10...O.K.
;
	JMP ERROR1      ;TOO MANY FILES
;
OP110	INC LDTND       ;NEW FILE
	LDA LA
	STA LAT,X       ;STORE LOGICAL FILE #
	LDA SA
	ORA #$60        ;MAKE SA AN SERIAL COMMAND
	STA SA
	STA SAT,X       ;STORE COMMAND #
	LDA FA
	STA FAT,X       ;STORE DEVICE #
;
;PERFORM DEVICE SPECIFIC OPEN TASKS
;
	BEQ OP175       ;IS KEYBOARD...DONE.
	CMP #3
	BEQ OP175       ;IS SCREEN...DONE.
	BCC OP150       ;ARE CASSETTES 1 & 2
;
	JSR OPENI       ;IS ON SERIAL...OPEN IT
	BCC OP175       ;BRANCH ALWAYS...DONE
;
;PERFORM TAPE OPEN STUFF
;
OP150	CMP #2
	BNE OP152
;
	JMP OPN232
;
OP152	JSR ZZZ         ;SEE IF TAPE BUFFER
	BCS OP155       ;YES
;
	JMP ERROR9      ;NO...DEALLOCATED
;
OP155	LDA SA
	AND #$F         ;MASK OFF COMMAND
	BNE OP200       ;NON ZERO IS TAPE WRITE
;
;OPEN CASSETE TAPE FILE TO READ
;
	JSR CSTE1       ;TELL "PRESS PLAY"
	BCS OP180       ;STOP KEY PRESSED
;
	JSR LUKING      ;TELL USER "SEARCHING"
;
	LDA FNLEN
	BEQ OP170       ;LOOKING FOR ANY FILE
;
	JSR FAF         ;LOOKING FOR NAMED FILE
	BCC OP171       ;FOUND IT!!!
	BEQ OP180       ;STOP KEY PRESSED
;
OP160	JMP ERROR4      ;FILE NOT FOUND
;
OP170	JSR FAH         ;GET ANY OLD HEADER
	BEQ OP180       ;STOP KEY PRESSED
	BCC OP171       ;ALL O.K.
	BCS OP160       ;FILE NOT FOUND...
;
;OPEN CASSETTE TAPE FOR WRITE
;
OP200	JSR CSTE2       ;TELL "PRESS PLAY AND RECORD"
	BCS OP180       ;STOP KEY PRESSED
	LDA #BDFH       ;DATA FILE HEADER TYPE
	JSR TAPEH       ;WRITE IT
;
;FINISH OPEN FOR TAPE READ/WRITE
;
OP171	LDA #BUFSZ-1    ;ASSUME FORCE READ
;
	LDY SA
	CPY #$60        ;OPEN FOR READ?
	BEQ OP172
;
;SET POINTERS FOR BUFFERING DATA
;
	LDY #0
	LDA #BDF        ;TYPE FLAG FOR BLOCK
	STA (TAPE1)Y    ;TO BEGIN OF BUFFER
	TYA
;
OP172	STA BUFPT       ;POINT TO DATA
OP175	CLC             ;FLAG GOOD OPEN
OP180	RTS             ;EXIT IN PEACE
.SKI 5
OPENI	LDA SA
	BMI OP50        ;NO SA...DONE
;
	LDY FNLEN
	BEQ OP50        ;NO FILE NAME...DONE
;
	LDA FA
	JSR LISTN       ;DEVICE LA TO LISTEN
;
	LDA SA
	ORA #$F0
	JSR SECND
;
	LDA STATUS      ;ANYBODY HOME?
	BPL OP35        ;YES...CONTINUE
;
;THIS ROUTINE IS CALLED BY OTHER
;KERNAL ROUTINES WHICH ARE CALLED
;DIRECTLY BY OS.  KILL RETURN
;ADDRESS TO RETURN TO OS.
;
	PLA
	PLA
	JMP ERROR5      ;DEVICE NOT PRESENT
;
OP35	LDA FNLEN
	BEQ OP45        ;NO NAME...DONE SEQUENCE
;
;SEND FILE NAME OVER SERIAL
;
	LDY #0
OP40	LDA (FNADR)Y
	JSR CIOUT
	INY
	CPY FNLEN
	BNE OP40
;
OP45	JSR UNLSN
;
OP50	CLC             ;NO  ERROR
	RTS
.PAGE 'OPEN RS232 FILE'
; OPN232 - OPEN AN RS-232 OR PARALLEL PORT FILE
;
; VARIABLES INITILIZED
;   BITNUM - # OF BITS TO BE SENT CALC FROM M51CTR
;   BAUDOF - BAUD RATE FULL
;   RSSTAT - RS-232 STATUS REG
;   M51CTR - 6551 CONTROL REG
;   M51CDR - 6551 COMMAND REG
;   M51AJB - USER TIME ####NOT IMPLEMENTED###
;
OPN232
;
; SET UP DDRB AND CB2 FOR RS-232
;
	LDA #%00000110  ;DDRB
	STA D1DDRB
	STA D1ORB       ;DTR,RTS HIGH
	LDA #%11101110  ;CASS OFF,TR MARK,CB1 HTL
	STA D1PCR
;
; PASS PRAMS TO M51REGS
	LDY #00
	STY RSSTAT      ;CLEAR STATUS
;
OPN020	CPY FNLEN       ;CHECK IF AT END OF FILENAME
	BEQ OPN025      ;YES...
;
	LDA (FNADR)Y    ;MOVE DATA
	STA M51CTR,Y    ;TO M51REGS
	INY
	CPY #4          ;ONLY 4 POSSIBLE PRAMS
	BNE OPN020
;
; CALC # OF BITS
;
OPN025	JSR BITCNT
	STX BITNUM
;
; CALC BAUD RATE
;
	LDA M51CTR
	AND #$0F
	BNE OPN010
;
; GET BAUD RATE FROM ELSE WHERE ###NEED CODE###
;
OPN010	ASL A           ;GET INDEX INTO BAUDO
	TAX
	LDA BAUDO-2,X
	ASL A           ;BAUDO*2+200 IS RATE
	TAY
	LDA BAUDO-1,X
	ROL A
	PHA
	TYA
	ADC #CBIT+CBIT  ;TABLE HAS CB1 ADJST OFFSET
	STA BAUDOF
	PLA
	ADC #0          ;GET CARRY IN
	STA BAUDOF+1
;
; CHECK FOR 3/X LINE RESPONSE
;
OPN030	LDA M51CDR      ;BIT 0 OF M51CDR
	LSR A
	BCC OPN050      ;...3 LINE
;
; CHECK FOR X LINE PROPER STATES
;
	LDA D2ORB
	ASL A
	BCS OPN050
	JMP DSRERR      ;NO DATA SET
;
; SET UP BUFFER POINTERS (DBE=DBS)
;
OPN050	LDA RIDBE
	STA RIDBS
	LDA RODBE
	STA RODBS
;
; ALLOCATE BUFFERS
;
OPN055	JSR GETTOP      ;GET MEMSIZ
	LDA RIBUF+1     ;IN ALLOCATION...
	BNE OPN060      ;ALREADY
	DEY             ;THERE GOES 256 BYTES
	STY RIBUF+1
	STX RIBUF
OPN060	LDA ROBUF+1     ;OUT ALLOCATION...
	BNE MEMTCF      ;ALREAY
	DEY             ;THERE GOES 256 BYTES
	STY ROBUF+1
	STX ROBUF
MEMTCF	SEC             ;SIGNAL TOP OF MEMORY CHANGE
	LDA #$F0
	JMP SETTOP      ;TOP CHANGED
.END
; RSR 8/25/80 ADD RS-232 CODE
; RSR 8/26/80 TOP OF MEMORY HANDLER
; RSR 8/29/80 ADD FILENAME TO M51REGS
; RSR 9/2/80 FIX ORDERING OF RS-232 ROUTINES
