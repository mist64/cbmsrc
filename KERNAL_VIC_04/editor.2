.PAG 'EDITOR.2'
;SCREEN SCROLL ROUTINE
;
SCROL	LDA SAL
	PHA
	LDA SAH
	PHA
	LDA EAL
	PHA
	LDA EAH
	PHA
;
;   S C R O L L   U P
;
SCRO0	LDX #$FF
	DEC TBLX
	DEC LSXP
	DEC LINTMP
SCR10	INX             ;GOTO NEXT LINE
	JSR SETPNT      ;POINT TO 'TO' LINE
	CPX #NLINES-1   ;DONE?
	BCS SCR41       ;BRANCH IF SO
;
	LDA LDTB2+1,X   ;SETUP FROM PNTR
	STA SAL
	LDA LDTB1+1,X
	JSR SCRLIN      ;SCROLL THIS LINE UP1
	BMI SCR10
;
SCR41
	JSR CLRLN
;
	LDX #0          ;SCROLL HI BYTE POINTERS
SCRL5	LDA LDTB1,X
	AND #$7F
	LDY LDTB1+1,X
	BPL SCRL3
	ORA #$80
SCRL3	STA LDTB1,X
	INX
	CPX #NLINES-1
	BNE SCRL5
;
	LDA LDTB1+NLINES-1
	ORA #$80
	STA LDTB1+NLINES-1
	LDA LDTB1       ;DOUBLE LINE?
	BPL SCRO0       ;YES...SCROLL AGAIN
;
	INC TBLX
	INC LINTMP
	LDA #$FB        ;CHECK FOR CONTROL KEY
	STA COLM        ;DROP LINE 2 ON PORT B
	LDA ROWS
	CMP #$FE        ;SLOW SCROLL KEY?(CONTROL)
	PHP             ;SAVE STATUS. RESTORE PORT B
	LDA #$F7        ;FOR STOP KEY CHECK
	STA COLM
	PLP
	BNE MLP42
;
	LDY #0
MLP4	NOP             ;DELAY
	DEX
	BNE MLP4
	DEY
	BNE MLP4
	STY NDX         ;CLEAR KEY QUEUE BUFFER
;
MLP42	LDX TBLX
;
	PLA
	STA EAH
	PLA
	STA EAL
	PLA
	STA SAH
	PLA
	STA SAL
;
	RTS
.PAG
NEWLIN
	LDX TBLX
BMT1	INX
	LDA LDTB1,X     ;FIND LAST DISPLAY LINE OF THIS LINE
	BPL BMT1
	STX LINTMP      ;FOUND IT
;GENERATE A NEW LINE
	CPX #NLINES-1   ;IS ONE LINE FROM BOTTOM?
	BEQ NEWLX       ;YES...JUST CLEAR LAST
	BCC NEWLX       ;<NLINES...INSERT LINE
	JSR SCROL       ;SCROLL EVERYTHING
	LDX LINTMP
	DEX
	DEC TBLX
	JMP WLOG30
NEWLX	LDA SAL
	PHA
	LDA SAH
	PHA
	LDA EAL
	PHA
	LDA EAH
	PHA
	LDX #NLINES
SCD10	DEX
	JSR SETPNT      ;SET UP TO ADDR
	CPX LINTMP
	BCC SCR40
	BEQ SCR40       ;BRANCH IF FINISHED
	LDA LDTB2-1,X   ;SET FROM ADDR
	STA SAL
	LDA LDTB1-1,X
	JSR SCRLIN      ;SCROLL THIS LINE DOWN
	BMI SCD10
SCR40
	JSR CLRLN
	LDX #NLINES-2
SCRD21
	CPX LINTMP      ;DONE?
	BCC SCRD22      ;BRANCH IF SO
	LDA LDTB1+1,X
	AND #$7F
	LDY LDTB1,X     ;WAS IT CONTINUED
	BPL SCRD19      ;BRANCH IF SO
	ORA #$80
SCRD19	STA LDTB1+1,X
	DEX
	BNE SCRD21
SCRD22
	LDX LINTMP
	JSR WLOG30
;
	PLA
	STA EAH
	PLA
	STA EAL
	PLA
	STA SAH
	PLA
	STA SAL
	RTS
;
; SCROLL LINE FROM SAL TO PNT
; AND COLORS FROM EAL TO USER
;
SCRLIN
	AND #$03        ;CLEAR ANY GARBAGE STUFF
	ORA HIBASE      ;PUT IN HIORDER BITS
	STA SAL+1
	JSR TOFROM      ;COLOR TO & FROM ADDRS
	LDY #LLEN-1
SCD20
	LDA (SAL)Y
	STA (PNT)Y
	LDA (EAL)Y
	STA (USER)Y
	DEY
	BPL SCD20
	RTS
;
; DO COLOR TO AND FROM ADDRESSES
; FROM CHARACTER TO AND FROM ADRS
;
TOFROM
	JSR SCOLOR
	LDA SAL         ;CHARACTER FROM
	STA EAL         ;MAKE COLOR FROM
	LDA SAL+1
	AND #$03
	ORA #>VICCOL
	STA EAL+1
	RTS
;
; SET UP PNT AND Y
; FROM .X
;
SETPNT	LDA LDTB2,X
	STA PNT
	LDA LDTB1,X
	AND #$03
	ORA HIBASE
	STA PNT+1
	RTS
;
; CLEAR THE LINE POINTED TO BY .X
;
CLRLN	LDY #LLEN-1
	JSR SETPNT
	JSR SCOLOR
CLR10	LDA #$20
	STA (PNT)Y
	LDA #WHITE      ;DO THE COLOR
	STA (USER)Y
	DEY
	BPL CLR10
	RTS
.SKI 5
;
;PUT A CHAR ON THE SCREEN
;
DSPP	TAY             ;SAVE CHAR
	LDA #2
	STA BLNCT       ;BLINK CURSOR
	JSR SCOLOR      ;SET COLOR PTR
	TYA             ;RESTORE COLOR
DSPP2	LDY PNTR        ;GET COLUMN
	STA (PNT)Y      ;CHAR TO SCREEN
	TXA
	STA (USER)Y     ;COLOR TO SCREEN
	RTS
.SKI 5
SCOLOR	LDA PNT         ;GENERATE COLOR PTR
	STA USER
	LDA PNT+1
	AND #$03
	ORA #>VICCOL    ;VIC COLOR RAM
	STA USER+1
	RTS
.PAG
KEY	JSR $FFEA       ;UPDATE JIFFY CLOCK
	LDA BLNSW       ;BLINKING CRSR ?
	BNE KEY4        ;NO
	DEC BLNCT       ;TIME TO BLINK ?
	BNE KEY4        ;NO
	LDA #20         ;RESET BLINK COUNTER
REPDO	STA BLNCT
	LDY PNTR        ;CURSOR POSITION
	LSR BLNON       ;CARRY SET IF ORIGINAL CHAR
	LDX GDCOL       ;GET CHAR ORIGINAL COLOR
	LDA (PNT)Y      ;GET CHARACTER
	BCS KEY5        ;BRANCH IF NOT NEEDED
;
	INC BLNON       ;SET TO 1
	STA GDBLN       ;SAVE ORIGINAL CHAR
	JSR SCOLOR
	LDA (USER)Y     ;GET ORIGINAL COLOR
	STA GDCOL       ;SAVE IT
	LDX COLOR       ;BLINK IN THIS COLOR
	LDA GDBLN       ;WITH ORIGINAL CHARACTER
;
KEY5	EOR #$80        ;BLINK IT
	JSR DSPP2       ;DISPLAY IT
;
KEY4	LDA D1ORA       ;GET CASSETTE SWITCHES
	AND #$40        ;IS SWITHC DOWN
	BEQ KEY3        ;BRANCH IF SO
;
	LDY #0
	STY CAS1        ;CASSETTE OFF SWITCH
;
	LDA D1PCR
	ORA #$02
	BNE KL24        ;BRANCH IF MOTOR IS OFF
;
KEY3	LDA CAS1
	BNE KL2
;
	LDA D1PCR
	AND #$FD        ;TURN MOTOR ON
;
KL24	BIT D1IER       ;CHECK RS-232 TRANSMITTER ON (T1)
	BVS KL2         ;YES...NO MANUAL CASS CONTROLS
	STA D1PCR
;
KL2	JSR SCNKEY      ;SCAN KEYBOARD
;
KPREND	BIT D2T1L       ;CLEAR INTERUPT FLAG
	PLA             ;RESTORE REGISTERS
	TAY
	PLA
	TAX
	PLA
	RTI             ;EXIT FROM IRQ ROUTINES
.SKI 3
; ****** GENERAL KEYBOARD SCAN ******
;
SCNKEY	LDA #$00
	STA SHFLAG
	LDY #64         ;LAST KEY INDEX
	STY SFDX        ;NULL KEY FOUND
	STA COLM        ;RAISE ALL LINES
	LDX ROWS        ;CHECK FOR A KEY DOWN
	CPX #$FF        ;NO KEYS DOWN?
	BEQ SCNOUT      ;BRANCH IF NONE
; LDA CRFAC ;CHECK FOR RESTORE BACKGROUND/BOARDER
; BPL DONTRE ;DON'T RESTORE IF PLUS
; LDA CRFAC+1 ;GET OLD VALUE
; STA VICREG+15 ;PUT IT BACK IN VIC
;DONTRE
; LDA #0
; STA CRFAC ;REMEMBER I CHANGED IT BACK
	LDA #$FE        ;START WITH 1ST COLUMN
	STA COLM
	LDY #0
	LDA #<MODE1
	STA KEYTAB
	LDA #>MODE1
	STA KEYTAB+1
SCN20	LDX #8          ;8 ROW KEYBOARD
	LDA ROWS
	CMP ROWS        ;DEBOUNCE KEYBOARD
	BNE SCN20
SCN30	LSR A           ;LOOK FOR KEY DOWN
	BCS CKIT        ;NONE
	PHA
	LDA (KEYTAB),Y  ;GET CHAR CODE
	CMP #$05
	BCS SPCK2       ;IF NOT SPECIAL KEY GO ON
	CMP #$03        ;COULD IT BE A STOP KEY?
	BEQ SPCK2       ;BRANCH IF SO
	ORA SHFLAG
	STA SHFLAG      ;PUT SHIFT BIT IN FLAG BYTE
	BPL CKUT
SPCK2
	STY SFDX        ;SAVE KEY NUMBER
CKUT	PLA
CKIT	INY
	CPY #65
	BCS CKIT1       ;BRANCH IF FINISHED
	DEX
	BNE SCN30
	SEC
	ROL COLM        ;NEXT COLUMN ON KEYBOARD
	BNE SCN20       ;ALWAYS BRANCH
CKIT1
	JMP (KEYLOG)    ;EVALUATE SHIFT FUNCTIONS
REKEY	LDY SFDX        ;GET KEY INDEX
	LDA (KEYTAB)Y   ;GET CHAR CODE
	TAX             ;SAVE THE CHAR
	CPY LSTX        ;SAME AS PREV CHAR INDEX?
	BEQ RPT10       ;YES
	LDY #$10        ;NO - RESET DELAY BEFORE REPEAT
	STY DELAY
	BNE CKIT2       ;ALWAYS
RPT10	AND #$7F        ;UNSHIFT IT
	BIT RPTFLG      ;CHECK FOR REPEAT DISABLE
	BMI RPT20       ;YES
	BVS SCNRTS
	CMP #$7F        ;NO KEYS ?
SCNOUT	BEQ CKIT2       ;YES - GET OUT
	CMP #$14        ;AN INST/DEL KEY ?
	BEQ RPT20       ;YES - REPEAT IT
	CMP #$20        ;A SPACE KEY ?
	BEQ RPT20       ;YES
	CMP #$1D        ;A CRSR LEFT/RIGHT ?
	BEQ RPT20       ;YES
	CMP #$11        ;A CRSR UP/DWN ?
	BNE SCNRTS      ;NO - EXIT
RPT20	LDY DELAY       ;TIME TO REPEAT ?
	BEQ RPT40       ;YES
	DEC DELAY
	BNE SCNRTS
RPT40	DEC KOUNT       ;TIME FOR NEXT REPEAT ?
	BNE SCNRTS      ;NO
	LDY #4          ;YES - RESET CTR
	STY KOUNT
	LDY NDX         ;NO REPEAT IF QUEUE FULL
	DEY
	BPL SCNRTS
CKIT2
	LDY SFDX        ;GET INDEX OF KEY
	STY LSTX        ;SAVE THIS INDEX TO KEY FOUND
	LDY SHFLAG      ;UPDATE SHIFT STATUS
	STY LSTSHF
CKIT3	CPX #$FF        ;A NULL KEY OR NO KEY ?
	BEQ SCNRTS      ;BRANCH IF SO
	TXA             ;NEED X AS INDEX SO...
	LDX NDX         ;GET # OF CHARS IN KEY QUEUE
	CPX XMAX        ;IRQ BUFFER FULL ?
	BCS SCNRTS      ;YES - NO MORE INSERT
PUTQUE
	STA KEYD,X      ;PUT RAW DATA HERE
	INX
	STX NDX         ;UPDATE KEY QUEUE COUNT
SCNRTS	LDA #$F7        ;SETUP PB3 FOR STOP KEY SENSE
	STA COLM
	RTS
.PAG
;
; SHIFT LOGIC
;
SHFLOG
	LDA SHFLAG
	CMP #$03        ;COMMODORE SHIFT COMBINATION?
	BNE KEYLG2      ;BRANCH IF NOT
	CMP LSTSHF      ;DID I DO THIS ALREADY
	BEQ SCNRTS      ;BRANCH IF SO
	LDA MODE
	BMI SHFOUT      ;DONT SHIFT IF ITS MINUS
	.BYT $EA,$EA    ;AND #$18 ;WHICH MODE?
	.BYT $EA,$EA    ;BEQ KATAON ;IF PET MODE TURNON KATAKANA
	.BYT $EA,$EA    ;LDA #0
	.BYT $EA,$EA,$EA;STA MODE ;SET PET MODE
	.BYT $EA,$EA,$EA;LDA VICREG+5 ;TURN ON PETSET
	.BYT $EA,$EA    ;AND #$FD ;CLEAR BIT 1
	.BYT $EA,$EA,$EA;STA VICREG+5
	.BYT $EA,$EA    ;BNE SHFOUT
SWITCH	LDA VICREG+5
	EOR #$02        ;TURN ON OTHER CASE
	STA VICREG+5    ;POINT THE VIC THERE
	.BYT $EA,$EA    ;LDA #8 ;SET KATAKANA MODE
	.BYT $EA,$EA    ;STA MODE
	JMP SHFOUT
KEYLG2
	ASL A
	CMP #$08        ;WAS IT A CONTROL KEY
	BCC NCTRL       ;BRANCH IF NOT
	LDA #6          ;ELSE USE TABLE #4
	.BYT $EA,$EA    ;BNE NOTKAT ;DO THE TABLE SHIFT
;
NCTRL
	.BYT $EA,$EA,$EA;LDX MODE
	.BYT $EA,$EA    ;BEQ NOTKAT
	.BYT $EA,$EA,$EA;LDX SHFLAG ;SEE IF CO.KEY
	.BYT $EA,$EA    ;CPX #2
	.BYT $EA,$EA    ;BNE NOTKAT ;BRANCH IF NOT
	.BYT $EA,$EA,$EA;CPX LSTSHF ;DID I DO THIS LAST TIME?
	.BYT $EA,$EA    ;BEQ SHFOUT ;BRANCH IF SO
	.BYT $EA,$EA,$EA;LDA MODE ;ELSE ...
	.BYT $EA,$EA    ;EOR #$18 ;SWITCH KATA-EXTENDED
	.BYT $EA,$EA,$EA;STA MODE
	.BYT $EA,$EA    ;BPL SHFOUT
NOTKAT
	.BYT $EA,$EA,$EA;ORA MODE ;ADD MODE TABLE OFFSET
	.BYT $EA,$EA    ;AND #$7F ;CLEAR
	TAX
	LDA KEYCOD,X
	STA KEYTAB
	LDA KEYCOD+1,X
	STA KEYTAB+1
SHFOUT
	JMP REKEY
.END
