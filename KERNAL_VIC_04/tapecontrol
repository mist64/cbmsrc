.PAG 'TAPE CONTROL'
JTP20	JSR ZZZ
	INC BUFPT
	LDY BUFPT
	CPY #BUFSZ
	RTS
.SKI 5
;STAYS IN ROUTINE D2T1LL PLAY SWITCH
;
CSTE1	JSR CS10
	BEQ CS25
	LDY #MS7-MS1    ;"PRESS PLAY..."
CS30	JSR MSG
CS40	JSR TSTOP       ;WATCH FOR STOP KEY
	JSR CS10        ;WATCH CASSETTE SWITCHES
	BNE CS40
	LDY #MS18-MS1   ;"OK"
	JMP MSG
.SKI 5
;SUBR RETURNS <> FOR CASSETTE SWITCH
;
CS10	LDA #$40        ;CHECK PORT
	BIT D1ORA       ;CLOSED?...
	BNE CS25        ;NO. . .
	BIT D1ORA       ;CHECK AGAIN TO DEBOUNCE
CS25	CLC             ;GOOD RETURN
	RTS
.SKI 5
;CHECKS FOR PLAY & RECORD
;
CSTE2	JSR CS10
	BEQ CS25
	LDY #MS8-MS1    ;"RECORD"
	BNE CS30
.SKI 5
;READ HEADER BLOCK ENTRY
;
RBLK	LDA #0
	STA STATUS
	STA VERCK
	JSR LDAD1
.SKI 3
;READ LOAD BLOCK ENTRY
;
TRD	JSR CSTE1       ;SAY 'PRESS PLAY'
	BCS TWRT3       ;STOP KEY PRESSED
	SEI
	LDA #0          ;CLEAR FLAGS...
	STA RDFLG
	STA SNSW1
	STA CMP0
	STA PTR1
	STA PTR2
	STA DPSW
	LDA #$82        ;ENABLE FOR CA1 IRQ...READ LINE
	LDX #14         ;POINT IRQ VECTOR TO READ
	BNE TAPE        ;JMP
.SKI 5
;WRITE HEADER BLOCK ENTRY
;
WBLK	JSR LDAD1
;
;WRITE LOAD BLOCK ENTRY
;
TWRT	LDA #20         ;BETWEEN BLOCK SHORTS
	STA SHCNH
TWRT2	JSR CSTE2       ;SAY 'PRESS PLAY & RECORD'
TWRT3	BCS STOP3       ;STOP KEY PRESSED
	SEI
	LDA #$A0        ;ENABLE T2 IRQS...WRITE TIME
	LDX #8          ;VECTOR IRQ TO WRTZ
.SKI 5
;START TAPE OPERATION ENTRY POINT
;
TAPE	LDY #$7F        ;KILL UNWANTED IRQ'S
	STY D2IER
	STA D2IER       ;TURN ON WANTED
; WAIT FOR RS-232 TO FINISH
	JSR RSP232
; MOVE IRQ TO IRQTEMP FOR CASS OPS
	LDA CINV
	STA IRQTMP
	LDA CINV+1
	STA IRQTMP+1
	JSR BSIV        ;GO CHANGE IRQ VECTOR
	LDA #2          ;FSBLK STARTS AT 2
	STA FSBLK
	JSR NEWCH       ;PREP LOCAL COUNTERS AND FLAGS
	LDA D1PCR       ;TURN MOTOR ON
	AND #$FD        ;LOW TURNS ON
	ORA #$0C        ;MAKE SURE
	STA D1PCR
	STA CAS1        ;FLAG INTERNAL CONTROL OF CASS MOTOR
	LDX #$FF        ;DELAY BETWEEN BLOCKS
TP32	LDY #$FF
TP35	DEY
	BNE TP35
	DEX
	BNE TP32
	STA D2T2H
	CLI
TP40	LDA IRQTMP+1    ;CHECK FOR INTERRUPT VECTOR...
	CMP CINV+1      ;...POINTING AT KEY ROUTINE
	CLC
	BEQ STOP3       ;...YES RETURN
	JSR TSTOP       ;...NO CHECK FOR STOP KEY
	LDA D2IFR
	AND #$40        ; CHECK FOR T1 IRQ
	BEQ TP40        ;...NO
	LDA D1T1L       ; KILL T1 IRQ REQUEST
	JSR UDTIM       ; STOP KEY CHECK
	JMP TP40        ;STAY IN LOOP UNTILL TAPES ARE DONE
.SKI 5
TSTOP	JSR STOP        ;STOP KEY DOWN?
	CLC             ;ASSUME NO STOP
	BNE STOP4       ;WE WERE RIGHT
;
;STOP KEY DOWN...
;
	JSR TNIF        ;TURN OFF CASSETTES
	SEC             ;FAILURE FLAG
	PLA             ;BACK ONE SQUARE...
	PLA
; LDA #0 ;STOP KEY FLAG
;
STOP3	LDA #0          ;DEALLOCATE IRQTMP
	STA IRQTMP+1    ;IF C-SET THEN STOP KEY
STOP4	RTS
.SKI 5
;
; STT1 - SET UP TIMEOUT WATCH FOR NEXT DIPOLE
;
STT1	STX TEMP        ;.X HAS CONSTANT FOR TIMEOUT
	LDA CMP0        ;CMP0*5
	ASL A
	ASL A
	CLC
	ADC CMP0
	CLC 
	ADC TEMP        ;ADJUST LONG BYTE COUNT
	STA TEMP
	LDA #0
	BIT CMP0        ;CHECK CMP0 ...
	BMI STT2        ;...MINUS, NO ADJUST
	ROL A           ;...PLUS SO ADJUST POS
STT2	ASL TEMP        ;MULTIPLY CORRECTED VALUE BY 4
	ROL A
	ASL TEMP
	ROL A
	TAX
STT3	LDA D2T2L       ;WATCH OUT FOR D2T2H ROLLOVER...
	CMP #21         ;...TIME FOR ROUTINE...!!!...
	BCC STT3        ;...TOO CLOSE SO WAIT UND2T1LL PAST
	ADC TEMP        ;CALCULATE AND...
	STA D2T1L       ;...STORE ADUSTED TIME COUNT
	TXA
	ADC D2T2H       ;ADJUST FOR HIGH TIME COUNT
	STA D2T1H
	CLI             ;ALLOW FOR RE-ENTRY CODE
	RTS
.END
; RSR 8/25/80 MODIFY I/O FOR MOD2 HARDWARE
