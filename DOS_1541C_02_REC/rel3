.PAGE 'REL3'
;********************************
;*
;*
;* RDREL
;*
;*
;********************************
;*
;*
;
RDREL
	LDA #LRF
	JSR TSTFLG
	BNE RD05        ;NO RECORD ERROR
;
RD10
	JSR GETPRE
	LDA BUFTAB,X
	CMP LSTCHR,Y
	BEQ RD40
;
	INC BUFTAB,X
	BNE RD20
;
	JSR NRBUF
RD15
	JSR GETPRE
RD20
	LDA (BUFTAB,X)
RD25
	STA CHNDAT,Y
	LDA #RNDRDY
	STA CHNRDY,Y
	LDA BUFTAB,X
	CMP LSTCHR,Y
	BEQ RD30
	RTS
RD30
	LDA #RNDEOI
	STA CHNRDY,Y
	RTS
RD40
	JSR NXTREC
	JSR GETPRE
	LDA DATA
	JMP RD25
;
RD05
	LDX LINDX       ;NO RECORD CHAR SET UP
	LDA #CR
	STA CHNDAT,X
	LDA #RNDEOI
	STA CHNRDY,X
;NO RECORD ERROR
	LDA #NOREC
	JSR CMDERR
;
.END
