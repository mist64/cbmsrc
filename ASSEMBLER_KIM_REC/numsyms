.PAGE 'NUMSYMS'

; CONVERT BASE 8,10,16 TO BINARY
; CARRY SET IF SUCCESSFUL CONVERSION, CARRY CLEAR IF ERROR.
; X MUST POINT TO CHARACTER

NUMBER	LDA #0
	STA KNVAL       ;VALUE OF NUMBER IS 0
	STA KNVAL+1
E10	LDA ICRD,X      ;CHARACTER
	JSR NUMRC       ;SEE IF NUMERIC
	BCC E20         ;NOT NUMERIC
	SEC
	SBC #48         ;REMOVE ZONE
	JMP E30         ;CHECK FOR VALID BASE
E20	JSR NALPH       ;CHAR ALPHABETIC
	BCC E40         ;NO...MEANS ERROR
	SEC
	SBC #55         ;ALPHA--REMOVE ZONE
E30	CMP KBASE       ;BASE VALID
	BCC E50         ;YES...VALID
E40	CLC             ;INVALID BASE
	RTS

E50	STA COLCNT
	TXA
	PHA             ;PUT POINTER ON STACK
	LDY KBASE       ;BASE
	CPY #2          ;BINARY?
	BNE E60         ;NO...
	LDX #1          ;BINARY...1 SHIFT
	JMP E90

E60	CPY #8          ;OCTAL
	BNE E70         ;NO...
	LDX #3          ;OCTAL--3 SHIFTS.
	JMP E90

E70	CPY #16         ;HEX
	BNE E80         ;NO...
	LDX #4          ;HEX--4 SHIFTS
	JMP E90

E80	CPY #10         ;DECIMAL
	BNE E40         ;NO... BASE IS INVALID
	LDA KNVAL
	STA TEMP
	LDA KNVAL+1
	STA TEMP+1
	LDX #3          ;DECIMAL--3 + 1 SHIFTS

E90	ASL KNVAL+1     ;LOW ORDER BITS
	ROL KNVAL       ;HIGH ORDER BITS
	BCC E100        ;NOT OVERFLOW
	LDA IFLAGS+1    ;OVERFLOW BIT
	ORA #8
	STA IFLAGS+1
E100	DEX             ;SHIFT COUNTER
	BNE E90         ;CONTINUE
	CPY #10         ;DECIMAL
	BNE E120        ;NO...
	ASL TEMP+1      ;DECIMAL DOES ANOTHER
	ROL TEMP
	BCC E115        ;DID NOT OVERFLOW
	LDA IFLAGS+1    ;SET OVERFLOW
	ORA #8
	STA IFLAGS+1
E115	LDA KNVAL+1     ;ADD TO FINISH
	CLC
	ADC TEMP+1
	STA KNVAL+1
	LDA KNVAL
	ADC TEMP
	STA KNVAL
	BCC E120        ;OVERFLOW?
	LDA IFLAGS+1
	ORA #8
	STA IFLAGS+1
E120	LDA COLCNT
	CLC
	ADC KNVAL+1
	STA KNVAL+1
	LDA $70
	ADC #0
	STA $70
	BCC E130        ;NO OVERFLOW
	LDA IFLAGS+1
	ORA #8
	STA IFLAGS+1
E130	PLA
	TAX
	INX
	DEC KLEN        ;LENGTH OF NUMBER
	BEQ E140        ;DONE
	JMP E10
E140	SEC             ;SUCCESS
	RTS





; SEARCH SYM TAB FOR CURRENT SYM. SEARCH IS LINEAR.
; CARRY SET IF FOUND. CARRY CLEAR IF NOT FOUND.

NFIND
L13C7	LDA STSAVE
	STA SYMTBL
	LDA STSAVE+1
	STA SYMTBL+1
	LDA #$01
	STA TOPPNT+1
	LDA #0
	STA TOPPNT
L13D5	CLV
	LDA TOPPNT
	CMP NOSYM
	BCC LE954
	BNE L13FC
	LDA TOPPNT+1
	CMP NOSYM+1
	BEQ LE954
	BCS L13FC
LE954	LDY #5
L13E4	LDA (SYMTBL),Y
	BPL LE95F
	AND #$7F
	BIT $EE81
LE95F	CMP ISYM,Y
	BNE L13FE
	DEY
	BPL L13E4
	LDY #6
	LDA (SYMTBL),Y
	STA KNVAL
	INY
	LDA (SYMTBL),Y
	STA KNVAL+1
	BVS L13FC
	RTS

L13FC	CLC
	RTS

L13FE	LDA SYMTBL
	CLC
	ADC #8
	STA SYMTBL
	BCC L1409
	INC SYMTBL+1
L1409	INC TOPPNT+1
	BNE L13D5
	INC TOPPNT
	JMP L13D5

NSERT	LDA SYMTBL+1
	CMP ISYEND+1
	BCC XXXD2
	BNE G110
	LDA SYMTBL
	CMP ISYEND
	BCS G110

XXXD2	LDY #5          ;PUT SYM INTO TABLE
G100	LDA ISYM,Y      ;SOURCE
	STA (SYMTBL),Y    ;DESTINATION
	DEY
	BPL G100        ;NOT DONE
	BVC LE9AB
	JSR $EA25
	BIT $EE81

;
; SYMBOL IS IN TABLE- DEFINE
;

LE9AB	LDY #6
	LDA KNVAL
	STA (SYMTBL),Y
	INY
	LDA KNVAL+1
	STA (SYMTBL),Y
	BVS G101
	INC NOSYM+1
	BNE G101
	INC NOSYM
G101	RTS

;
; OVERFLOWED SYMBOL TABLE
;

G110	LDX #$37
	JSR $ED1F
	JSR WSCRN
	JMP DONE



; SEARCH OPCODE TAB FOR OPCODE
; CARRY SET IF OPCODE FOUND, CARRY CLEAR IF OPCODE NOT FOUND.

NOPFND	LDA $EFD2
	STA PNT1
	LDA $EFD3
	STA PNT1+1
	LDX #0
XXXE3	LDY #2
XXXE2	LDA ISYM,Y
	CMP (PNT1),Y
	BNE XXXE1
	DEY
	BPL XXXE2
	LDA KTMPL,X     ;TEMPLATE
	STA JOPTEM
	LDA KCODE,X     ;BASE OPCODE
	STA JOPBAS
	RTS

;
; NO MATCH
;

XXXE1	LDA PNT1
	CLC
	ADC #3
	STA PNT1
	BCC XXXE4
	INC PNT1+1
XXXE4	INX
	CPX #57
	BMI XXXE3

SBNO	CLC             ;OPCODE NOT FOUND
	RTS

.END
