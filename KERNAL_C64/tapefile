.PAG 'TAPE FILES'
;FAH -- FIND ANY HEADER
;
;READS TAPE DEVICE UNTIL ONE OF FOLLOWING
;BLOCK TYPES FOUND: BDFH--BASIC DATA
;FILE HEADER, BLF--BASIC LOAD FILE
;FOR SUCCESS CARRY IS CLEAR ON RETURN.
;FOR FAILURE CARRY IS SET ON RETURN.
;IN ADDITION ACCUMULATOR IS 0 IF STOP
;KEY WAS PRESSED.
;
FAH	LDA VERCK       ;SAVE OLD VERIFY
	PHA
	JSR RBLK        ;READ TAPE BLOCK
	PLA
	STA VERCK       ;RESTORE VERIFY FLAG
	BCS FAH40       ;READ TERMINATED
;
	LDY #0
	LDA (TAPE1)Y    ;GET HEADER TYPE
;
	CMP #EOT        ;CHECK END OF TAPE?
	BEQ FAH40       ;YES...FAILURE
;
	CMP #BLF        ;BASIC LOAD FILE?
	BEQ FAH50       ;YES...SUCCESS
;
	CMP #PLF        ;FIXED LOAD FILE?
	BEQ FAH50       ;YES...SUCCESS
;
	CMP #BDFH       ;BASIC DATA FILE?
	BNE FAH         ;NO...KEEP TRYING
;
FAH50	TAX             ;RETURN FILE TYPE IN .X
	BIT MSGFLG      ;PRINTING MESSAGES?
	BPL FAH45       ;NO...
;
	LDY #MS17-MS1   ;PRINT "FOUND"
	JSR MSG
;
;OUTPUT COMPLETE FILE NAME
;
	LDY #5
FAH55	LDA (TAPE1)Y
	JSR BSOUT
	INY
	CPY #21
	BNE FAH55
;
FAH56	LDA STKEY       ;KEY  DOWN ON LAST ROW...
	CMP #$FF
	BEQ FAH56       ;NO KEY...STAY IN LOOP
;
FAH45	CLC             ;SUCCESS FLAG
	DEY             ;MAKE NONZERO FOR OKAY RETURN
;
FAH40	RTS
.SKI 5
;TAPEH--WRITE TAPE HEADER
;ERROR IF TAPE BUFFER DE-ALLOCATED
;CARRY CLEAR IF O.K.
;
TAPEH	STA T1
;
;DETERMINE ADDRESS OF BUFFER
;
	JSR ZZZ
	BCC TH40        ;BUFFER WAS DE-ALLOCATED
;
;PRESERVE START AND END ADDRESSES
;FOR CASE OF HEADER FOR LOAD FILE
;
	LDA STAH
	PHA
	LDA STAL
	PHA
	LDA EAH
	PHA
	LDA EAL
	PHA
;
;PUT BLANKS IN TAPE BUFFER
;
	LDY #BUFSZ-1
	LDA #' 
BLNK2	STA (TAPE1)Y
	DEY
	BNE BLNK2
;
;PUT BLOCK TYPE IN HEADER
;
	LDA T1
	STA (TAPE1)Y
;
;PUT START LOAD ADDRESS IN HEADER
;
	INY
	LDA STAL
	STA (TAPE1)Y
	INY
	LDA STAH
	STA (TAPE1)Y
;
;PUT END LOAD ADDRESS IN HEADER
;
	INY
	LDA EAL
	STA (TAPE1)Y
	INY
	LDA EAH
	STA (TAPE1)Y
;
;PUT FILE NAME IN HEADER
;
	INY
	STY T2
	LDY #0
	STY T1
TH20	LDY T1
	CPY FNLEN
	BEQ TH30
	LDA (FNADR)Y
	LDY T2
	STA (TAPE1)Y
	INC T1
	INC T2
	BNE TH20
;
;SET UP START AND END ADDRESS OF HEADER
;
TH30	JSR LDAD1
;
;SET UP TIME FOR LEADER
;
	LDA #$69
	STA SHCNH
;
	JSR TWRT2       ;WRITE HEADER ON TAPE
;
;RESTORE START AND END ADDRESS OF
;LOAD FILE.
;
	TAY             ;SAVE ERROR CODE IN .Y
	PLA
	STA EAL
	PLA 
	STA EAH
	PLA
	STA STAL
	PLA
	STA STAH
	TYA             ;RESTORE ERROR CODE FOR RETURN
;
TH40	RTS
.SKI 5
;FUNCTION TO RETURN TAPE BUFFER
;ADDRESS IN TAPE1
;
ZZZ	LDX TAPE1       ;ASSUME TAPE1
	LDY TAPE1+1
	CPY #>BUF       ;CHECK FOR ALLOCATION...
;...[TAPE1+1]=0 OR 1 MEANS DEALLOCATED
;...C CLR => DEALLOCATED
	RTS
.SKI 5
LDAD1	JSR ZZZ         ;GET PTR TO CASSETTE
	TXA
	STA STAL        ;SAVE START LOW
	CLC
	ADC #BUFSZ      ;COMPUTE POINTER TO END
	STA EAL         ;SAVE END LOW
	TYA
	STA STAH        ;SAVE START HIGH
	ADC #0          ;COMPUTE POINTER TO END
	STA EAH         ;SAVE END HIGH
	RTS
.SKI 5
FAF	JSR FAH         ;FIND ANY HEADER
	BCS FAF40       ;FAILED
;
;SUCCESS...SEE IF RIGHT NAME
;
	LDY #5          ;OFFSET INTO TAPE HEADER
	STY T2
	LDY #0          ;OFFSET INTO FILE NAME
	STY T1
FAF20	CPY FNLEN       ;COMPARE THIS MANY
	BEQ FAF30       ;DONE
;
	LDA (FNADR)Y
	LDY T2
	CMP (TAPE1)Y
	BNE FAF         ;MISMATCH--TRY NEXT HEADER
	INC T1
	INC T2
	LDY T1
	BNE FAF20       ;BRANCH ALWAYS
;
FAF30	CLC             ;SUCCESS FLAG
FAF40	RTS
.END
; RSR  4/10/82 ADD KEY DOWN TEST IN FAH...
