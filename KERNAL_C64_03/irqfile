.PAGE 'IRQFILE - DISPATCHER'
; SIMIRQ - SIMULATE AN IRQ (FOR CASSETTE READ)
;  ENTER BY A JSR SIMIRQ
;
SIMIRQ	PHP
	PLA             ;FIX THE BREAK FLAG
	AND #$EF
	PHA
; PULS - CHECKS FOR REAL IRQ'S OR BREAKS
;
PULS	PHA
	TXA
	PHA
	TYA
	PHA
	TSX
	LDA $104,X      ;GET OLD P STATUS
	AND #$10        ;BREAK FLAG?
	BEQ PULS1       ;...NO
	JMP (CBINV)     ;...YES...BREAK INSTR
PULS1	JMP (CINV)      ;...IRQ
.SKI 5
.PAG "IRQFILE-PATCHES 6/82"
; PCINT - ADD UNIVERSAL TO CINIT
;
PCINT	JSR CINT
P0010	LDA VICREG+18   ;CHECK RASTER COMPARE FOR ZERO
	BNE P0010       ;IF IT'S ZERO THEN CHECK VALUE
	LDA VICREG+25   ;GET RASTER IRQ VALUE
	AND #$01
	STA PALNTS      ;PLACE IN PAL/NTSC INDICATOR
	JMP IOKEYS
;
; PIOKEY - ADD UNIVERSAL TO IOKEYS
;
PIOKEY	LDA #$81        ;ENABLE T1 IRQ'S
	STA D1ICR
	LDA D1CRA
	AND #$80        ;SAVE ONLY TOD BIT
	ORA #%00010001  ;ENABLE TIMER1
	STA D1CRA
	JMP CLKLO       ;RELEASE THE CLOCK LINE***901227-03***
	*=$E500-20
;
; BAUDOP - BAUD RATE TABLE FOR PAL
;   .985248E6/BAUD-RATE/2-100
;
BAUDOP	.WOR 9853-CBIT ;50 BAUD
	.WOR 6568-CBIT ;75 BAUD
	.WOR 4478-CBIT ;110 BAUS
	.WOR 3660-CBIT ;134.6 BAUD
	.WOR 3284-CBIT ;150 BAUD
	.WOR 1642-CBIT ;300 BAUD
	.WOR 821-CBIT  ;600 BAUD
	.WOR 411-CBIT  ;1200 BAUD
	.WOR 274-CBIT  ;1800 BAUD
	.WOR 205-CBIT  ;2400 BAUD
.PAGE "IRQFILE -  PATCHES"
	*=$E500-32      ;(20-12)
; FPATCH - TAPE FILENAME TIMEOUT
;
FPATCH	ADC #2          ;TIME IS (8 TO 13 SEC OF DISPLAY)
FPAT00	LDY STKEY       ;CHECK FOR KEY DOWN ON LAST ROW...
	INY
	BNE FPAT01      ;KEY...EXIT LOOP
	CMP TIME+1      ;WATCH TIMER
	BNE FPAT00
FPAT01	RTS
.SKI 5
	*=$E500-38      ;(32-6)
; CPATCH - FIX TO CLEAR LINE...MODIFIED 901227-03
;  PREVENTS WHITE CHARACTER FLASH...
CPATCH                  ;ALWAYS CLEAR TO CURRENT FOREGND COLOR
	LDA COLOR
	STA (USER)Y
	RTS
.SKI 5
	*=$E500-45      ;(38-7)
; PRTYP - RS232 PARITY PATCH...ADDED 901227-03
;
PRTYP	STA RINONE      ;GOOD RECEIVER START...DISABLE FLAG
	LDA #1          ;SET PARITY TO 1 ALWAYS
	STA RIPRTY
	RTS
.END
