.PAG 'CODE19'
LOG	JSR SIGN
	BEQ LOGERR
	BPL LOG1
LOGERR	JMP FCERR
LOG1	LDA FACEXP
	SBC #@177
	PHA
	LDA #@200
	STA FACEXP
	LDA #<SQR05
	LDY #>SQR05
	JSR FADD
	LDA #<SQR20
	LDY #>SQR20
	JSR FDIV
	LDA #<FONE
	LDY #>FONE
	JSR FSUB
	LDA #<LOGCN2
	LDY #>LOGCN2
	JSR POLYX
	LDA #<NEGHLF
	LDY #>NEGHLF
	JSR FADD
	PLA
	JSR FINLOG
	LDA #<LOG2
	LDY #>LOG2
FMULT	JSR CONUPK
FMULTT	BNE *+5
	JMP MULTRT
	JSR MULDIV
	LDA #0
	STA RESHO
	STA RESMOH
	STA RESMO
	STA RESLO
	LDA FACOV
	JSR MLTPLY
	LDA FACLO
	JSR MLTPLY
	LDA FACMO
	JSR MLTPLY
	LDA FACMOH
	JSR MLTPLY
	LDA FACHO
	JSR MLTPL1
	JMP MOVFR
MLTPLY	BNE *+5
	JMP MULSHF
MLTPL1	LSR A
	ORA #@200
MLTPL2	TAY
	BCC MLTPL3
	CLC
	LDA RESLO
	ADC ARGLO
	STA RESLO
	LDA RESMO
	ADC ARGMO
	STA RESMO
	LDA RESMOH
	ADC ARGMOH
	STA RESMOH
	LDA RESHO
	ADC ARGHO
	STA RESHO
MLTPL3	ROR RESHO
	ROR RESMOH
	ROR RESMO
	ROR RESLO
	ROR FACOV
	TYA
	LSR A
	BNE MLTPL2
MULTRT	RTS
CONUPK	STA INDEX1
	STY INDEX1+1
	LDY #3+ADDPRC
	LDA (INDEX1)Y
	STA ARGLO
	DEY
	LDA (INDEX)Y
	STA ARGMO
	DEY
	LDA (INDEX1)Y
	STA ARGMOH
	DEY
	LDA (INDEX1)Y
	STA ARGSGN
	EOR FACSGN
	STA ARISGN
	LDA ARGSGN
	ORA #@200
	STA ARGHO
	DEY
	LDA (INDEX1)Y
	STA ARGEXP
	LDA FACEXP
	RTS
MULDIV	LDA ARGEXP
MLDEXP	BEQ ZEREMV
	CLC
	ADC FACEXP
	BCC TRYOFF
	BMI GOOVER
	CLC
	.BYT $2C
TRYOFF	BPL ZEREMV
	ADC #@200
	STA FACEXP
	BNE *+5
	JMP ZEROML
	LDA ARISGN
	STA FACSGN
	RTS
MLDVEX	LDA FACSGN
	EOR #@377
	BMI GOOVER
ZEREMV	PLA
	PLA
	JMP ZEROFC
GOOVER	JMP OVERR
MUL10	JSR MOVAF
	TAX
	BEQ MUL10R
	CLC
	ADC #2
	BCS GOOVER
FINML6	LDX #0
	STX ARISGN
	JSR FADDC
	INC FACEXP
	BEQ GOOVER
MUL10R	RTS
TENC	.BYT @204,@40,0,0,0
DIV10	JSR MOVAF
	LDA #<TENC
	LDY #>TENC
	LDX #0
FDIVF	STX ARISGN
	JSR MOVFM
	JMP FDIVT
FDIV	JSR CONUPK
FDIVT	BEQ DV0ERR
	JSR ROUND
	LDA #0
	SEC
	SBC FACEXP
	STA FACEXP
	JSR MULDIV
	INC FACEXP
	BEQ GOOVER
	LDX #253-ADDPRC
	LDA #1
DIVIDE	LDY ARGHO
	CPY FACHO
	BNE SAVQUO
	LDY ARGMOH
	CPY FACMOH
	BNE SAVQUO
	LDY ARGMO
	CPY FACMO
	BNE SAVQUO
	LDY ARGLO
	CPY FACLO
SAVQUO	PHP
	ROL A
	BCC QSHFT
	INX
	STA RESLO,X
	BEQ LD100
	BPL DIVNRM
	LDA #1
QSHFT	PLP
	BCS DIVSUB
SHFARG	ASL ARGLO
	ROL ARGMO
	ROL ARGMOH
	ROL ARGHO
	BCS SAVQUO
	BMI DIVIDE
	BPL SAVQUO
DIVSUB	TAY
	LDA ARGLO
	SBC FACLO
	STA ARGLO
	LDA ARGMO
	SBC FACMO
	STA ARGMO
	LDA ARGMOH
	SBC FACMOH
	STA ARGMOH
	LDA ARGHO
	SBC FACHO
	STA ARGHO
	TYA
	JMP SHFARG
LD100	LDA #@100
	BNE QSHFT
DIVNRM	ASL A
	ASL A
	ASL A
	ASL A
	ASL A
	ASL A
	STA FACOV
	PLP
	JMP MOVFR
DV0ERR	LDX #ERRDVO
	JMP ERROR
.END
