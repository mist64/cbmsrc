.PAG 'CODE15'
	STA INDEX1
	STX INDEX1+1
SVAR	CPX ARYTAB+1
	BNE SVARGO
	CMP ARYTAB
	BEQ ARYVAR
SVARGO	JSR DVARS
	BEQ SVAR
ARYVAR	STA ARYPNT
	STX ARYPNT+1
	LDA #STRSIZ
	STA FOUR6
ARYVA2	LDA ARYPNT
	LDX ARYPNT+1
ARYVA3	CPX STREND+1
	BNE ARYVGO
	CMP STREND
	BNE *+5
	JMP GRBPAS
ARYVGO	STA INDEX1
	STX INDEX1+1
	LDY #1-ADDPRC
	LDA (INDEX1)Y
	TAX
	INY
	LDA (INDEX1)Y
	PHP
	INY
	LDA (INDEX1)Y
	ADC ARYPNT
	STA ARYPNT
	INY
	LDA (INDEX1)Y
	ADC ARYPNT+1
	STA ARYPNT+1
	PLP
	BPL ARYVA2
	TXA
	BMI ARYVA2
	INY
	LDA (INDEX1)Y
	LDY #0
	ASL A
	ADC #5
	ADC INDEX1
	STA INDEX1
	BCC ARYGET
	INC INDEX1+1
ARYGET	LDX INDEX1+1
ARYSTR	CPX ARYPNT+1
	BNE GOGO
	CMP ARYPNT
	BEQ ARYVA3
GOGO	JSR DVAR
	BEQ ARYSTR
DVARS	LDA (INDEX1)Y
	BMI DVARTS
	INY
	LDA (INDEX1)Y
	BPL DVARTS
	INY
DVAR	LDA (INDEX1)Y
	BEQ DVARTS
	INY
	LDA (INDEX1)Y
	TAX
	INY
	LDA (INDEX1)Y
	CMP FRETOP+1
	BCC DVAR2
	BNE DVARTS
	CPX FRETOP
	BCS DVARTS
DVAR2	CMP GRBTOP+1
	BCC DVARTS
	BNE DVAR3
	CPX GRBTOP
	BCC DVARTS
DVAR3	STX GRBTOP
	STA GRBTOP+1
	LDA INDEX1
	LDX INDEX1+1
	STA GRBPNT
	STX GRBPNT+1
	LDA FOUR6
	STA SIZE
DVARTS	LDA FOUR6
	CLC
	ADC INDEX1
	STA INDEX1
	BCC GRBRTS
	INC INDEX1+1
GRBRTS	LDX INDEX1+1
	LDY #0
	RTS
GRBPAS	LDA GRBPNT+1
	ORA GRBPNT
	BEQ GRBRTS
	LDA SIZE
	AND #4
	LSR A
	TAY
	STA SIZE
	LDA (GRBPNT)Y
	ADC LOWTR
	STA HIGHTR
	LDA LOWTR+1
	ADC #0
	STA HIGHTR+1
	LDA FRETOP
	LDX FRETOP+1
	STA HIGHDS
	STX HIGHDS+1
	JSR BLTUC
	LDY SIZE
	INY
	LDA HIGHDS
	STA (GRBPNT)Y
	TAX
	INC HIGHDS+1
	LDA HIGHDS+1
	INY
	STA (GRBPNT)Y
	JMP FNDVAR
CAT	LDA FACLO
	PHA
	LDA FACMO
	PHA
	JSR EVAL
	JSR CHKSTR
	PLA
	STA STRNG1
	PLA
	STA STRNG1+1
	LDY #0
	LDA (STRNG1)Y
	CLC
	ADC (FACMO)Y
	BCC SIZEOK
	LDX #ERRLS
	JMP ERROR
SIZEOK	JSR STRINI
	JSR MOVINS
	LDA DSCPNT
	LDY DSCPNT+1
	JSR FRETMP
	JSR MOVDO
	LDA STRNG1
	LDY STRNG1+1
	JSR FRETMP
	JSR PUTNEW
	JMP TSTOP
MOVINS	LDY #0
	LDA (STRNG1)Y
	PHA
	INY
.END
