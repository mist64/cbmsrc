.PAG 'CODE21'
QINT	LDA FACEXP
	BEQ CLRFAC
	SEC
	SBC #ADDPR8+@230
	BIT FACSGN
	BPL QISHFT
	TAX
	LDA #@377
	STA BITS
	JSR NEGFCH
	TXA
QISHFT	LDX #FAC
	CMP #$F9
	BPL QINT1
	JSR SHIFTR
	STY BITS
QINTRT	RTS
QINT1	TAY
	LDA FACSGN
	AND #@200
	LSR FACHO
	ORA FACHO
	STA FACHO
	JSR ROLSHF
	STY BITS
	RTS
INT	LDA FACEXP
	CMP #ADDPR8+@230
	BCS INTRTS
	JSR QINT
	STY FACOV
	LDA FACSGN
	STY FACSGN
	EOR #@200
	ROL A
	LDA #@230+8
	STA FACEXP
	LDA FACLO
	STA INTEGR
	JMP FADFLT
CLRFAC	STA FACHO
	STA FACMOH
	STA FACMO
	STA FACLO
	TAY 
INTRTS	RTS
FIN	LDY #@0
	LDX #@11+ADDPRC
FINZLP	STY DECCNT,X
	DEX
	BPL FINZLP
	BCC FINDGQ
	CMP #'-
	BNE QPLUS
	STX SGNFLG
	BEQ FINC
QPLUS	CMP #'+
	BNE FIN1
FINC	JSR CHRGET
FINDGQ	BCC FINDIG
FIN1	CMP #'.
	BEQ FINDP
	CMP #'E
	BNE FINE
	JSR CHRGET
	BCC FNEDG1
	CMP #MINUTK
	BEQ FINEC1
	CMP #'-
	BEQ FINEC1
	CMP #PLUSTK
	BEQ FINEC
	CMP #'+
	BEQ FINEC
	BNE FINEC2
FINEC1	ROR EXPSGN
FINEC	JSR CHRGET
FNEDG1	BCC FINEDG
FINEC2	BIT EXPSGN
	BPL FINE
	LDA #0
	SEC
	SBC TENEXP
	JMP FINE1
FINDP	ROR DPTFLG
	BIT DPTFLG
	BVC FINC
FINE	LDA TENEXP
FINE1	SEC
	SBC DECCNT
	STA TENEXP
	BEQ FINQNG
	BPL FINMUL
FINDIV	JSR DIV10
	INC TENEXP
	BNE FINDIV
	BEQ FINQNG
FINMUL	JSR MUL10
	DEC TENEXP
	BNE FINMUL
FINQNG	LDA SGNFLG
	BMI NEGXQS
	RTS
NEGXQS	JMP NEGOP
FINDIG	PHA
	BIT DPTFLG
	BPL FINDG1
	INC DECCNT
FINDG1	JSR MUL10
	PLA
	SEC
	SBC #'0
	JSR FINLOG
	JMP FINC
FINLOG	PHA
	JSR MOVAF
	PLA
	JSR FLOAT
	LDA ARGSGN
	EOR FACSGN
	STA ARISGN
	LDX FACEXP
	JMP FADDT
FINEDG	LDA TENEXP
	CMP #@12
	BCC MLEX10
	LDA #@144
	BIT EXPSGN
	BMI MLEXMI
	JMP OVERR
MLEX10	ASL A
	ASL A
	CLC
	ADC TENEXP
	ASL A
	CLC
	LDY #0
	ADC (TXTPTR)Y
	SEC
	SBC #'0
MLEXMI	STA TENEXP
	JMP FINEC
.END
