.PAG 'CODE1'
OMERR	LDX #ERROM
ERROR	JMP (IERROR)
NERROR	TXA
	ASL A
	TAX
	LDA ERRTAB-2,X
	STA INDEX1
	LDA ERRTAB-1,X
	STA INDEX1+1
	JSR CLSCHN
	LDA #0
	STA CHANNL
ERRCRD	JSR CRDO
	JSR OUTQST
	LDY #0
GETERR	LDA (INDEX1)Y
	PHA
	AND #127
	JSR OUTDO
	INY
	PLA
	BPL GETERR
	JSR STKINI
	LDA #<ERR
	LDY #>ERR
ERRFIN	JSR STROUT
	LDY CURLIN+1
	INY
	BEQ READY
	JSR INPRT
.SKI 5
READY	LDA #<REDDY
	LDY #>REDDY
	JSR STROUT
	LDA #$80        ;DIRECT MESSAGES ON
	JSR SETMSG      ;FROM KERNAL
.SKI 5
MAIN	JMP (IMAIN)
NMAIN	JSR INLIN
	STX TXTPTR
	STY TXTPTR+1
	JSR CHRGET
	TAX
	BEQ MAIN
	LDX #255
	STX CURLIN+1
	BCC MAIN1
	JSR CRUNCH
	JMP GONE
MAIN1	JSR LINGET
	JSR CRUNCH
	STY COUNT
	JSR FNDLIN
	BCC NODEL
	LDY #1
	LDA (LOWTR)Y
	STA INDEX1+1
	LDA VARTAB
	STA INDEX1
	LDA LOWTR+1
	STA INDEX2+1
	LDA LOWTR
	DEY
	SBC (LOWTR)Y 
	CLC
	ADC VARTAB
	STA VARTAB
	STA INDEX2
	LDA VARTAB+1
	ADC #255
	STA VARTAB+1
	SBC LOWTR+1
	TAX
	SEC
	LDA LOWTR
	SBC VARTAB
	TAY
	BCS QDECT1
	INX
	DEC INDEX2+1
QDECT1	CLC
	ADC INDEX1
	BCC MLOOP
	DEC INDEX1+1
	CLC
MLOOP	LDA (INDEX1)Y
	STA (INDEX2)Y
	INY 
	BNE MLOOP
	INC INDEX1+1
	INC INDEX2+1
	DEX
	BNE MLOOP
NODEL	JSR RUNC
	JSR LNKPRG
	LDA BUF
	BEQ MAIN
	CLC
	LDA VARTAB
	STA HIGHTR 
	ADC COUNT
	STA HIGHDS
	LDY VARTAB+1
	STY HIGHTR+1
	BCC NODELC
	INY
NODELC	STY HIGHDS+1
	JSR BLTU
	LDA LINNUM
	LDY LINNUM+1
	STA BUF-2
	STY BUF-1
	LDA STREND
	LDY STREND+1
	STA VARTAB
	STY VARTAB+1
	LDY COUNT
	DEY
STOLOP	LDA BUF-4,Y
	STA (LOWTR)Y
	DEY
	BPL STOLOP
FINI	JSR RUNC
	JSR LNKPRG
	JMP MAIN
LNKPRG	LDA TXTTAB
	LDY TXTTAB+1
	STA INDEX
	STY INDEX+1
	CLC 
CHEAD	LDY #1
	LDA (INDEX)Y
	BEQ LNKRTS
	LDY #4
CZLOOP	INY
	LDA (INDEX)Y
	BNE CZLOOP
	INY
	TYA
	ADC INDEX
	TAX
	LDY #0
	STA (INDEX)Y
	LDA INDEX+1
	ADC #0
	INY
	STA (INDEX)Y
	STX INDEX
	STA INDEX+1
	BCC CHEAD
LNKRTS	RTS
.SKI 5
;FUNCTION TO GET A LINE ONE CHARACTER AT
;A TIME FROM THE INPUT CHANNEL AND
;BUILD IT IN THE INPUT BUFFER.
;
INLIN	LDX #0
;
INLINC	JSR INCHR
	CMP #13         ;A CARRIAGE RETURN?
	BEQ FININ1      ;YES...DONE BUILD
;
	STA BUF,X       ;PUT IT AWAY
	INX
	CPX #BUFLEN     ;MAX CHARACTER LINE?
	BCC INLINC      ;NO...O.K.
;
	LDX #ERRLS      ;STRING TOO LONG ERROR
	JMP ERROR
;
FININ1	JMP FININL
.END
