.PAG 'CODE12'
N32768	.BYT 144,128,0,0,0
FLPINT	JSR AYINT
	LDA FACMO
	LDY  FACLO
	RTS
INTIDX	JSR CHRGET
	JSR FRMEVL
POSINT	JSR CHKNUM
	LDA FACSGN
	BMI NONONO
AYINT	LDA FACEXP
	CMP #144
	BCC QINTGO
	LDA #<N32768
	LDY #>N32768
	JSR FCOMP
NONONO	BNE FCERR
QINTGO	JMP QINT
ISARY	LDA DIMFLG
	ORA INTFLG
	PHA
	LDA VALTYP
	PHA
	LDY #0
INDLOP	TYA
	PHA
	LDA VARNAM+1
	PHA
	LDA VARNAM
	PHA
	JSR INTIDX
	PLA
	STA VARNAM
	PLA
	STA VARNAM+1
	PLA
	TAY
	TSX
	LDA 258,X
	PHA
	LDA 257,X
	PHA
	LDA INDICE
	STA 258,X
	LDA INDICE+1
	STA 257,X
	INY
	JSR CHRGOT
	CMP #44
	BEQ INDLOP
	STY COUNT
	JSR CHKCLS
	PLA
	STA VALTYP
	PLA
	STA INTFLG
	AND #127
	STA DIMFLG
	LDX ARYTAB
	LDA ARYTAB+1
LOPFDA	STX LOWTR
	STA LOWTR+1
	CMP STREND+1
	BNE LOPFDV
	CPX STREND
	BEQ NOTFDD
LOPFDV	LDY #0
	LDA (LOWTR)Y
	INY
	CMP VARNAM
	BNE NMARY1
	LDA VARNAM+1
	CMP (LOWTR)Y
	BEQ GOTARY
NMARY1	INY
	LDA (LOWTR)Y
	CLC
	ADC LOWTR
	TAX
	INY
	LDA (LOWTR)Y
	ADC LOWTR+1
	BCC LOPFDA
BSERR	LDX #ERRBS
	.BYT $2C
FCERR	LDX #ERRFC
ERRGO3	JMP ERROR
GOTARY	LDX #ERRDD
	LDA DIMFLG
	BNE ERRGO3
	JSR FMAPTR
	LDA COUNT
	LDY #4
	CMP (LOWTR)Y
	BNE BSERR
	JMP GETDEF
NOTFDD	JSR FMAPTR
	JSR REASON
	LDY #0
	STY CURTOL+1
	LDX #5
	LDA VARNAM
	STA (LOWTR)Y
	BPL NOTFLT
	DEX
NOTFLT	INY
	LDA VARNAM+1
	STA (LOWTR)Y
	BPL STOMLT
	DEX
	DEX
STOMLT	STX CURTOL
	LDA COUNT
	INY
	INY
	INY
	STA (LOWTR)Y
LOPPTA	LDX #11
	LDA #0
	BIT DIMFLG
	BVC NOTDIM
	PLA
	CLC
	ADC #1
	TAX
	PLA
	ADC #0
NOTDIM	INY
	STA (LOWTR)Y
	INY
	TXA
	STA (LOWTR)Y
	JSR UMULT
	STX CURTOL
	STA CURTOL+1
	LDY INDEX
	DEC COUNT
	BNE LOPPTA
	ADC ARYPNT+1
	BCS OMERR1
	STA ARYPNT+1
	TAY
	TXA
	ADC ARYPNT
	BCC GREASE
	INY
	BEQ OMERR1
GREASE	JSR REASON
	STA STREND
	STY STREND+1
	LDA #0
	INC CURTOL+1
.END
